% Case - (roşie, albă, albastră, galbenă, verde)
% Nationalitate - (britanic, suedez, danez, norvegian, german)
% Bautura - (lapte, bere, ceai, apă, cafea)
% tigari - (PallMall, Winfield, Marlboro, Dunhill, Rothmans)
% animal - (pasăre, caine, cal, pisică, peşti)
% REGULI!!!!!!!!
% Britanicul locuieşte în casa roşie.
% Norvegianul locuieşte lângă casa albastră.
% Casa verde se află în stânga casei albe.
% Locatarul casei verzi bea cafea.
% Locatarul casei din mijloc bea lapte.
% Locatarul din casa galbenă fumează Dunhill.
% Norvegianul locuieşte în prima casă.
% Suedezul are un câine.
% Persoana care fumează Pall Mall are o pasăre.
% Fumătorul de Marlboro locuieşte lângă cel care are o pisică.
% Fumătorul de Winfield bea bere.
% Locatarul care are un cal locuieşte lângă cel care fumează Dunhill.
% Germanul fumează Rothmans.
% Fumătorul de Marlboro are un vecin care bea apă.
% Raspundeti (printr-un program Prolog) la intrebarea cine are peşti

% REZOLVARE
% membru(X,L) - X este in lista L
membru(X,[X|_]).
membru(X,[_|T]) :- membru(X,T).
% dreapta(X,Y,L) : Y se afla in dreapta lui X in lista L
dreapta(L, R, [L | [R | _]]).
dreapta(L, R, [_ | Rest]) :- dreapta(L, R, Rest).
% langa(X,Y,L) : X se afla langa Y in lista L
langa(X, Y, List) :- dreapta(X, Y, List).
langa(X, Y, List) :- dreapta(Y, X, List).

lab2(Case, Prop_Peste) :-
                 =(Case, [[casa, norvegian, _, _, _, _], _, [casa, _, _, _, lapte, _], _, _]),
                 membru([casa, britanic, _, _, _, rosu], Case),
				 langa([casa, norvegian, _, _, _, _], [casa, _, _, _, _, albastru], Case),
				 dreapta([casa, _, _, _, _, verde], [casa, _, _, _, _, alb], Case),
				 membru([casa, _, _, _, cafea, verde], Case),
				 membru([casa, _, _, dunhill, _, galben], Case),
				 membru([casa, suedez, caine, _, _, _], Case),
				 membru([casa, _, pasare, pallmall, _, _], Case),
				 langa([casa, _, _, marlboro, _, _], [casa, _, pisica, _, _, _], Case),
				 membru([casa, _, _, winfield, bere, _], Case),
				 langa([casa, _, _, dunhill, _, _], [casa, _, cal, _, _, _], Case),
				 membru([casa, german, _, rothmans, _, _], Case),
				 langa([casa, _, _, marlboro, _, _], [casa, _, _, _, apa, _], Case),
    			 membru([casa, Prop_Peste, peste, _, _, _], Case),
    			 membru([casa, danez, _, _, _, _], Case),
    			 membru([casa, _, _, _, ceai, _], Case).